plugins {
    id 'java'
    id 'io.quarkus'

    id "net.ltgt.errorprone" version "3.1.0"
	id "jacoco"
	id "org.sonarqube" version "6.0.1.5171"
    id "org.spdx.sbom" version "0.9.0"
}

repositories {
    mavenCentral()
}

dependencies {
    implementation enforcedPlatform("${quarkusPlatformGroupId}:${quarkusPlatformArtifactId}:${quarkusPlatformVersion}")
    
    implementation 'io.quarkus:quarkus-rest'
    implementation 'io.quarkus:quarkus-container-image-docker'
    implementation 'io.quarkus:quarkus-smallrye-health'
    implementation 'io.quarkus:quarkus-kubernetes'
    implementation 'io.quarkus:quarkus-arc'
    implementation "io.quarkus:quarkus-rest-client"

    errorprone "com.google.errorprone:error_prone_core:2.19.1"

    testImplementation 'io.quarkus:quarkus-junit5'
    testImplementation 'io.rest-assured:rest-assured'
}

group 'de.qaware.cloud'
version '1.0.0'

java {
    sourceCompatibility = JavaVersion.VERSION_21
    targetCompatibility = JavaVersion.VERSION_21
}

test {
    systemProperty "java.util.logging.manager", "org.jboss.logmanager.LogManager"
    finalizedBy jacocoTestReport // report is always generated after tests run
}
compileJava {
    options.errorprone.enabled = true
    options.encoding = 'UTF-8'
    options.compilerArgs << '-parameters'
}

compileTestJava {
    options.errorprone.enabled = false
    options.encoding = 'UTF-8'
}

jacocoTestReport {
    dependsOn test // tests are required to run before generating the report
	reports {
		xml.required = true
	}
}

sonar {
  properties {
    property "sonar.projectKey", "lreimer_iso27001-secure-se"
    property "sonar.organization", "lreimer"
    property "sonar.host.url", "https://sonarcloud.io"
  }
}

spdxSbom {
  targets {
    // create a target named "release",
    // this is used for the task name (spdxSbomForRelease)
    // and output file (release.spdx.json)
    create("release") {
        scm {
            uri.set("https://github.com/lreimer/iso27001-secure-se.git")
        }    

        document {
            name.set("Example SPDX document")
            namespace.set("https://qaware.de/spdx/3f8b7a2e-4c1d-4b5e-9a7f-2d6e8f1c4b3a")
            creator.set("Person:Mario-Leander Reimer")
        }
    }
  }
}